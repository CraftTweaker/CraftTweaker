buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "1.12-" + ext.craftTweakerVersion
archivesBaseName = project.projectDir.name

jar {
    manifest {
        attributes 'FMLAT': 'crafttweaker_at.cfg'
    }
}

minecraft {
    version = forgeVersion1120
    runDir = "CraftTweaker2-MC1120-Main/eclipse"
    mappings = mappings1120
}
repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
}

def jei_version = ext.jei_version
dependencies {
    compile project(':ZenScript')
    compile project(':CraftTweaker2-API')
    deobfProvided "mezz.jei:jei_1.12:${jei_version}:api"
    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_1.12:${jei_version}"
}

processResources
        {

            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + System.getenv("local_maven"))
            pom {
                groupId = project.group
                version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'CraftTweaker'
                    url 'https://github.com/jaredlll08/CraftTweaker'
                    scm {
                        url 'https://github.com/jaredlll08/CraftTweaker.git'
                        connection 'scm:git:git@github.com:jaredlll08/CraftTweaker.git'
                        developerConnection 'scm:git:git@github.com:jaredlll08/CraftTweaker.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/jaredlll08/CraftTweaker/issues'
                    }
                    developers {
                        developer {
                            id 'Jared'
                            name 'Jared'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}
